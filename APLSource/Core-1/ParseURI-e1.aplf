 ParseURI←{
     ⍝ ⍺ ←→ Result Space
     ⍝ ⍵ ←→ URI
     fos←{¯2↑(⊂''),⍵⊆⍨~<\⍺=⍵}
     bos←{2↑(⍵⊆⍨~<\⍺=⍵),⊂''}
     ⍺←⎕NS''
     ⍺.URI←⍵
     i←↑⍸'://'⍷⍵
     ⍺.Scheme←i↑⍵
     ⍺.AuthRes←⍵↓⍨i+3×i≠0
     ⍺.Authority←⍺.AuthRes↑⍨⍺.AuthRes⍳'/'
     ⍺.Resource←⍺.AuthRes↓⍨≢⍺.Authority
     ⍺.UserInfo r←'@'fos ⍺.Authority
     ⍺.(Host Port)←':'bos r
     ⍺.(Subdomain Domain)←'.'fos ⍺.Host
     ⍺.TLD←↑⌽'.'bos ⍺.Domain
     i j←⍺.Resource⍳'?#'
     ⍺.Path←(i⌊j)↑⍺.Resource
     ⍺.Query←(0⌈j-i+1)↑(i+1)↓⍺.Resource
     ⍺.Fragment←(j+1)↓⍺.Resource
     s←{('/'≠⍵)⊂⍵}⍺.Path
     k←s⍳¨';'
     ⍺.Segments←PercentDecode¨k↑¨s
     m←(k+1)↓¨s
     ⍺.MatrixParameters←ParseParameters¨m
     ⍺.QueryParameters←'&'ParseParameters ⍺.Query
     ⍺.Display←{{⍵,⍪⍎¨⍵}⎕NL ¯2}
     ⍺
 }

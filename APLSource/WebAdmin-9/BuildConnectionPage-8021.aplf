 BuildConnectionPage←{
     ⍝ ⍵ ←→ Request Message
     ⍝ r ←→ Response
     s←⍵.Connection.Server
     n←2⊃⍵.Segments
     c←{6::0 ⋄ s.Connections⍎⍵}n
     c=0:⍵ Respond 404
     d←BuildTemplate(↑⌽⍵.Segments)⍵.RequestTarget
     me←A.GetMain d
     _←me H.NewSectionDelete'Delete (Kill) this connection.'⍵.RequestTarget
     m←↓⍉⊃c.MessageLog
     v←⊂'Method'(0⊃m)
     v,←⊂'URI'(1⊃m)
     v,←⊂'StatusCode'(⍕¨2⊃m)
     cs←⎕NS¨(≢v)⍴⊂''
     cs.HeaderValue←0⊃¨v
     cs.BodyValues←1⊃¨v
     cs.Class←'tc' 'tc' 'tc'
     _←me H.NewSectionTable'Completed Messages'cs
     r←##.Core.NewResponse ⍵
     r.Content←d
     r
 }
